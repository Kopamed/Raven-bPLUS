plugins {
    id 'java'
    id 'net.minecraftforge.gradle.forge' version 'd583588'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

version = "1.1.0"
group= "club.sk1er.keystrokes"
archivesBaseName = "[1.8.9] BetterKeystrokes V"

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    compile fileTree(dir: "lib", include: "discord-rpc.jar")
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    replace('${GRADLE_MOD_ID}', mod_id)
    replace('${GRADLE_MOD_NAME}', mod_name)
    replace('${GRADLE_MOD_DESCRIPTION}', mod_description)
    replace('${GRADLE_MOD_VERSION}', mod_version)
    makeObfSourceJar = false
    //clientJvmArgs += "-Dfml.coreMods.load=keystrokesmod.tweaker.FMLLoadingPlugin"
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                "FMLCorePlugin": "keystrokesmod.tweaker.FMLLoadingPlugin",
                "ForceLoadAsMod": true,
                "FMLCorePluginContainsFMLMod": true,
                "ModSide": "CLIENT"
        )
    }
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property 'name', mod_name
    inputs.property 'modid', mod_id
    inputs.property 'description', mod_description
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand (
                'version':project.version,
                'mcversion':project.minecraft.version,
                'name':mod_name,
                'modid': mod_id,
                'description':mod_description
        )

    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources
reobfJar.dependsOn tasks.shadowJar